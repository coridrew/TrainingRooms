namespace TrainingRooms.Model;

fact Individual {
key:
    string anonymousId;
}

fact Installation {
key:
	unique;
}

fact Venue {
key:
	unique;

query:
	Room* rooms {
		Room r : r.venue = this
			where not r.isDeleted
	}

	Group* groups {
		Group g : g.venue = this
			where not g.isDeleted
	}
}

fact Day {
key:
	date when;
}

fact Room {
key:
	unique;
	Venue venue;

mutable:
	string name;

query:
	bool isDeleted {
		exists RoomDelete d : d.room = this
	}
}

fact RoomDelete {
key:
	Room room;
}

fact Group {
key:
	unique;
	Venue venue;

mutable:
	string name;
	string imageUrl;

query:
	bool isDeleted {
		exists GroupDelete d : d.group = this
	}
}

fact GroupDelete {
key:
	Group group;
}

fact Schedule {
key:
	Room room;
	Day day;

query:
	Event* events {
		Event e : e.schedule = this
	}
}

fact Event {
key:
	unique;
	Schedule schedule;
	Group group;
	time when;

query:
	bool isDeleted {
		exists EventDelete d : d.deletedEvent = this
	}
}

fact EventDelete {
key:
	Event deletedEvent;
}